######################################################################################
# Following:
# - https://besjournals.onlinelibrary.wiley.com/doi/10.1111/j.1365-2664.2009.01664.x
# - https://esajournals.onlinelibrary.wiley.com/doi/10.1002/eap.2293
# - https://www.sciencedirect.com/science/article/abs/pii/S0006320709004819

model{

  ## Community (group) level hyperpriors
  for (g in 1:G) {
    # Occurrence
    mu.u[g]    ~ dnorm(0, 1/2.25^2)
    sigma.u[g] ~ dt(0, 1/2.25^2, 1) T(0,)  # standard deviation
    tau.u[g] <- sigma.u[g]^-2         # precision
    
    # Covariate effects on occurrence
    mu.alpha1[g]    ~ dnorm(0, 1/2.25^2) # dnorm(0, 1/2.25^2) per Broms et al. 2016
    mu.alpha2[g]    ~ dnorm(0, 1/2.25^2)
    mu.alpha3[g]    ~ dnorm(0, 1/2.25^2)
    mu.alpha4[g]    ~ dnorm(0, 1/2.25^2)
    mu.alpha5[g]    ~ dnorm(0, 1/2.25^2)
    # dt(0, 1/2.25^2, 1) T(0,) Half-cauchy (truncated t distribution with 1 dof) per Broms et al. 2016
    sigma.alpha1[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    sigma.alpha2[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    sigma.alpha3[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    sigma.alpha4[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    sigma.alpha5[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    tau.alpha1[g] <- sigma.alpha1[g]^-2
    tau.alpha2[g] <- sigma.alpha2[g]^-2
    tau.alpha3[g] <- sigma.alpha3[g]^-2
    tau.alpha4[g] <- sigma.alpha4[g]^-2
    tau.alpha5[g] <- sigma.alpha5[g]^-2
    
    # Detection
    mu.v[g]    ~ dnorm(0, 1/2.25^2)
    sigma.v[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    tau.v[g] <- sigma.v[g]^-2
  
    # Covariate effects on detection
    mu.beta1[g]    ~ dnorm(0, 1/2.25^2)
    mu.beta2[g]    ~ dnorm(0, 1/2.25^2)
    mu.beta3[g]    ~ dnorm(0, 1/2.25^2)
    sigma.beta1[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    sigma.beta2[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    sigma.beta3[g] ~ dt(0, 1/2.25^2, 1) T(0,)
    tau.beta1[g]  <- sigma.beta1[g]^-2
    tau.beta2[g]  <- sigma.beta2[g]^-2
    tau.beta3[g]  <- sigma.beta3[g]^-2
  
  } # G groups

  ## Species level priors
  for (i in 1:I) {
      
      # Occupancy
      u[i]      ~ dnorm(mu.u[group[i]], tau.u[group[i]])
      alpha1[i] ~ dnorm(mu.alpha1[group[i]],tau.alpha1[group[i]])
      alpha2[i] ~ dnorm(mu.alpha2[group[i]],tau.alpha2[group[i]])
      alpha3[i] ~ dnorm(mu.alpha3[group[i]],tau.alpha3[group[i]])
      alpha4[i] ~ dnorm(mu.alpha4[group[i]],tau.alpha4[group[i]])
      alpha5[i] ~ dnorm(mu.alpha5[group[i]],tau.alpha5[group[i]])

      # Unconfirmed true positive detection
      v[i]     ~ dnorm(mu.v[group[i]], tau.v[group[i]])
      beta1[i] ~ dnorm(mu.beta1[group[i]],tau.beta1[group[i]])
      beta2[i] ~ dnorm(mu.beta2[group[i]],tau.beta2[group[i]])
      beta3[i] ~ dnorm(mu.beta3[group[i]],tau.beta3[group[i]])
  
  } # I species
  
  for (i in 1:I) { # for each species i
      for (j in 1:J) { # for each site j
          
              # Ecological process model for latent occurrence z
              logit(psi[j,i]) <- u[i] +
                alpha1[i]*x_alpha1[j] +
                alpha2[i]*x_alpha2[j] +
                alpha3[i]*x_alpha3[j] +
                alpha4[i]*x_alpha4[j] +
                alpha5[i]*x_alpha5[j]

              z[j,i] ~ dbern(psi[j,i])
              
              for (k in 1:K[j]) { # for each survey k during season t at site j
    
                  ## Observation model, assumming no false positives (e.g. Zipkin et al. 2009)
                  
                  # p11 is true-positive detection probability given z = 1
                  logit(p11[j,k,i]) <- v[i] +
                    beta1[i]*x_beta1[j,k] +
                    beta2[i]*x_beta2[j,k] +
                    beta3[i]*x_beta3[j,k]
                
                  p[j,k,i] <- z[j,i] * p11[j,k,i]
                  
                  # Observed outcome
                  y[j,k,i] ~ dbern(p[j,k,i])
                  
                  # Simulated replicate for posterior predictive checks
                  y_sim[j,k,i] ~ dbern(p[j,k,i])

                  # Deviance (Broms et al. 2016)
                  d_obs[j,k,i] <- z[j,i] * (y[j,k,i] * log(p[j,k,i]) + (1 - y[j,k,i]) * log(1 - p[j,k,i]))
                  d_sim[j,k,i] <- z[j,i] * (y_sim[j,k,i] * log(p[j,k,i]) + (1 - y_sim[j,k,i]) * log(1 - p[j,k,i]))
      
              } # K surveys
              
              # Pad jagged array of deviance values with zeros to enable sum below
              for (k in (K[j]+1):Kmax) {
                  d_obs[j,k,i] <- 0
                  d_sim[j,k,i] <- 0
              }
              
      } # J sites
  } # I species
  
  ## Derived quantities
  
  # Deviance of simulated and observed data for posterior predictive check
  D_obs <- -2 * sum(d_obs[1:J,1:Kmax,1:I])
  D_sim <- -2 * sum(d_sim[1:J,1:Kmax,1:I])
}
