######################################################################################
# Following:
# - https://besjournals.onlinelibrary.wiley.com/doi/10.1111/j.1365-2664.2009.01664.x
# - https://esajournals.onlinelibrary.wiley.com/doi/10.1002/eap.2293
# - https://www.sciencedirect.com/science/article/abs/pii/S0006320709004819

model{

  ## Community (group) level hyperpriors
  for (g in 1:G) {
    # Occurrence
    psi.mean[g] ~ dunif(0,1)                         # probability scale
    mu.u[g] <- log(psi.mean[g]) - log(1-psi.mean[g]) # logit scale
    sigma.u[g] ~ dunif(0,5)                          # standard deviation
    tau.u[g] <- pow(sigma.u[g],-2)                   # precision
    
    # Covariate effects on occurrence
    mu.alpha1[g] ~ dnorm(0,0.01)
    sigma.alpha1[g] ~ dunif(0,5)
    tau.alpha1[g] <- pow(sigma.alpha1[g],-2)
    mu.alpha2[g] ~ dnorm(0,0.01)
    sigma.alpha2[g] ~ dunif(0,5)
    tau.alpha2[g] <- pow(sigma.alpha2[g],-2)
    mu.alpha3[g] ~ dnorm(0,0.01)
    sigma.alpha3[g] ~ dunif(0,5)
    tau.alpha3[g] <- pow(sigma.alpha3[g],-2)
    
    # Detection (unconfirmed true-positive)
    v.mean[g]  ~ dunif(0,1)
    mu.v[g]  <- log(v.mean[g]) - log(1-v.mean[g])
    sigma.v[g] ~ dunif(0,5)
    tau.v[g] <- pow(sigma.v[g],-2)
  
    # Covariate effects on detection (unconfirmed true-positive)
    mu.beta1[g]    ~ dnorm(0,0.01)
    mu.beta2[g]    ~ dnorm(0,0.01)
    mu.beta3[g]    ~ dnorm(0,0.01)
    sigma.beta1[g] ~ dunif(0,5)
    sigma.beta2[g] ~ dunif(0,5)
    sigma.beta3[g] ~ dunif(0,5)
    tau.beta1[g]  <- pow(sigma.beta1[g],-2)
    tau.beta2[g]  <- pow(sigma.beta2[g],-2)
    tau.beta3[g]  <- pow(sigma.beta3[g],-2)
  
  } # G groups

  ## Species level priors
  for (i in 1:I) {
      
      # Occupancy
      u[i]      ~ dnorm(mu.u[group[i]], tau.u[group[i]])
      alpha1[i] ~ dnorm(mu.alpha1[group[i]],tau.alpha1[group[i]])
      alpha2[i] ~ dnorm(mu.alpha2[group[i]],tau.alpha2[group[i]])
      alpha3[i] ~ dnorm(mu.alpha3[group[i]],tau.alpha3[group[i]])
  
      # Unconfirmed true positive detection
      v[i]     ~ dnorm(mu.v[group[i]], tau.v[group[i]])
      beta1[i] ~ dnorm(mu.beta1[group[i]],tau.beta1[group[i]])
      beta2[i] ~ dnorm(mu.beta2[group[i]],tau.beta2[group[i]])
      beta3[i] ~ dnorm(mu.beta3[group[i]],tau.beta3[group[i]])
  
  } # I species
  
  for (i in 1:I) { # for each species i
      for (j in 1:J) { # for each site j
          
              # Ecological process model for latent occurrence z
              logit(psi[j,i]) <- u[i] + alpha1[i]*x_alpha1[j] + alpha2[i]*x_alpha2[j] + alpha3[i]*x_alpha3[j]
              z[j,i] ~ dbern(psi[j,i])
              
              for (k in 1:K[j]) { # for each survey k during season t at site j
    
                  ## Observation model, assumming no false positives (e.g. Zipkin et al. 2009)
                  
                  # p11 is true-positive detection probability given z = 1
                  logit(p11[j,k,i]) <- v[i] + beta1[i]*x_beta1[j,k] + beta2[i]*x_beta2[j,k] + beta3[i]*x_beta3[j,k]
                
                  p[j,k,i] <- z[j,i] * p11[j,k,i]
                  
                  # Observed outcome
                  y[j,k,i] ~ dbern(p[j,k,i])
                  
                  # Simulated replicate for posterior predictive checks
                  y_sim[j,k,i] ~ dbern(p[j,k,i])

                  # Deviance (Broms et al. 2016)
                  d_obs[j,k,i] <- y[j,k,i] * log(p[j,k,i]) + (1 - y[j,k,i]) * log(1 - p[j,k,i])
                  d_sim[j,k,i] <- y_sim[j,k,i] * log(p[j,k,i]) + (1 - y_sim[j,k,i]) * log(1 - p[j,k,i])
      
              } # K surveys
              
              # Pad jagged array of deviance values with zeros to enable sum below
              for (k in (K[j]+1):Kmax) {
                  d_obs[j,k,i] <- 0
                  d_sim[j,k,i] <- 0
              }
              
      } # J sites
  } # I species
  
  ## Derived quantities
  
  # Deviance of simulated and observed data for posterior predictive check
  D_obs <- -2 * sum(d_obs[1:J,1:Kmax,1:I])
  D_sim <- -2 * sum(d_sim[1:J,1:Kmax,1:I])
}
